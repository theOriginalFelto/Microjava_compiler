package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

// import java.io.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

init with {: 
	errorDetected = false;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, VOID, PRINT, READ, RETURN, NEW, CONST;
terminal PLUS, PLUSPLUS, MINUS, MINUSMINUS, EQUAL, MUL, DIV, MOD;
terminal DOT, SEMI, COMMA, FINDANY;
terminal LPAREN, RPAREN, LBRACE, RBRACE, LSQUARE, RSQUARE;
terminal Boolean BOOLEANCONST;
terminal Integer NUMBER;
terminal String IDENT;
terminal Character CHARCONST;

nonterminal VarDeclList, MethodDeclList, ConstDecl, MultipleConstDecl;

nonterminal FormPars, FormalParamList, FormalParamDecl;
//nonterminal ActualPars, ActualParamList; 
nonterminal StatementList, Statement, DesignatorStatement;
nonterminal Addop, Mulop;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program, ProgName, MethodDecl, MethodTypeName, Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Term, Expr, Factor, ConstValue;
nonterminal rs.etf.pp1.symboltable.concepts.Struct VarDeclSemi, VarDecl, MultVarDecl, MultArrDecl;

Program ::= (Program) PROG ProgName:p VarDeclList LBRACE MethodDeclList RBRACE;

ProgName ::= (ProgName) IDENT:programName;


VarDeclList ::= (VarDeclarations) VarDeclList VarDeclSemi
				|
				(ConstDeclarations) VarDeclList ConstDecl SEMI
				|
				(NoVarDecl)/* epsilon */ 
				;
				
VarDeclSemi ::= (VarDeclarationWithSemi) VarDecl SEMI
				|
				(VarDeclError) error SEMI:safe
				{: parser.report_error("Greska pri deklarisanju globalnih promjenljivih. Izvrsen oporavak do ; u liniji " + safeleft, null); :}
				;

VarDecl ::= (MultipleVarDecl) VarDecl COMMA MultVarDecl
			|
			(MultipleArrayDecl) VarDecl COMMA MultArrDecl
			|
			(VarDeclaration) Type:varType IDENT:varName
			|
			(ArrayDecl) Type:varType IDENT:varName LSQUARE RSQUARE
			;
			
MultVarDecl ::= (MultVarDecl) IDENT:varName;

MultArrDecl ::= (MultArrDecl) IDENT:varName LSQUARE RSQUARE;


ConstDecl ::= (MultipleConstDeclList) CONST MultipleConstDecl;

MultipleConstDecl ::= (MultipleConstDeclarations) MultipleConstDecl COMMA IDENT:constVarName EQUAL ConstValue
					|
					(ConstDeclaration) Type:constVarType IDENT:constVarName EQUAL ConstValue
					;

ConstValue ::= (ConstNumber) NUMBER
				|
				(ConstBoolean) BOOLEANCONST
				|
				(ConstChar) CHARCONST
				;

Type ::= (Type) IDENT:typeName;



MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDecl
					|
					(NoMethodDecl)/* epsilon */
					;

MethodDecl ::= (MethodDeclaration) MethodTypeName
			   LPAREN FormPars RPAREN VarDeclList LBRACE StatementList RBRACE 
			   ;

MethodTypeName ::= (MethodTypeDecl) Type:retType IDENT:methName
				|
				(MethodVoidTypeDecl) VOID IDENT:methName
				;

FormPars ::= (FormParams) FormalParamList 
				| 
				(NoFormParam) /* epsilon */ 
				;

FormalParamList ::= (FormalParamDecls) FormalParamList COMMA FormalParamDecl
					|
					(SingleFormalParamDecl) FormalParamDecl
					;
					
FormalParamDecl ::= (FormalVarDecl) Type:varType IDENT:varName
					|
					(FormalArrDecl) Type:varType IDENT:varName LSQUARE RSQUARE
					;
					

StatementList ::= (Statements) StatementList Statement 
					|
				  (NoStmt) /* epsilon */
				  ;

Statement ::= (DesigStatement) DesignatorStatement SEMI 
			  |
			  (ErrorStatement) error SEMI:safe
			  {: parser.report_error("Greska kod iskaza dodjele. Izvrsen oporavak do ; u liniji " + safeleft, null); :}
			  |
			  (PrintStatement) PRINT LPAREN Expr RPAREN SEMI 
			  |
			  (PrintStatementNumber) PRINT LPAREN Expr COMMA NUMBER:numVal RPAREN SEMI 
			  |
			  (ReadStatement) READ LPAREN Designator:designator RPAREN SEMI 
			  |
			  (ReturnExprStatement) RETURN Expr:expr SEMI
			  |
			  (ReturnNoExprStatement) RETURN SEMI
			  |
			  (FindAnyStatement) Designator:boolVar EQUAL Designator:array DOT FINDANY LPAREN Expr:expr RPAREN SEMI
			  ;
			  
DesignatorStatement ::= (AssignStatement) Designator:lvalue EQUAL Expr:rvalue
						|
						(IncrementStatement) Designator:lvalue PLUSPLUS
						|
						(DecrementStatement) Designator:lvalue MINUSMINUS
//						|
//						(FuntionCall) Designator:function LPAREN ActualPars RPAREN SEMI
						;
			  
Designator ::= (DesignatorArray) Designator LSQUARE Expr:expr RSQUARE
				|
				(DesignatorVar) IDENT:var
				;

			  
Expr ::= (AddExpr) Expr:expr Addop Term:term
			|
			(TermExpr) Term:term
			|
			(TermExprMinus) MINUS Term:term
		 	;

Term ::= (MulTerm) Term:term Mulop Factor:factor
			|
			(FactorTerm) Factor:factor
			;

Factor ::= (FactorConst) ConstValue:val
			|
//		   (FactorFuncCall) Designator:func LPAREN ActualPars RPAREN
//		   |
		   (FactorVar) Designator:var
		   |
		   (FactorNewArray) NEW Type:type LSQUARE Expr:expr RSQUARE
		   |
		   (FactorParenthesisExpression) LPAREN Expr:expr RPAREN
		   ;

//ActualPars ::= (Actuals) ActualParamList 
//				| 
//			   (NoActuals) /* epsilon */ 
//			   ;
			   
//ActualParamList ::= (ActualParams) ActualParamList COMMA Expr
//					|
//					(ActualParam) Expr
//					;
					

Addop ::= (PlusOperator) PLUS
			|
			(MinusOperator) MINUS
			;
			
Mulop ::= (MulOperator) MUL
			|
			(DivOperator) DIV
			|
			(ModOperator) MOD
			;